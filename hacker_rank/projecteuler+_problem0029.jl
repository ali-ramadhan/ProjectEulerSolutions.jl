# HackerRank ProjectEuler+ Problem 29: Distinct Powers
# https://www.hackerrank.com/contests/projecteuler/challenges/euler029/problem
#
# Project Euler: https://projecteuler.net/problem=29
# Solution: https://aliramadhan.me/blog/project-euler/problem-0029/
#
# Problem Statement:
#   Consider all integer combinations of a^b for 2 ≤ a ≤ 5 and 2 ≤ b ≤ 5:
#
#   2^2 = 4,  2^3 = 8,   2^4 = 16,  2^5 = 32
#   3^2 = 9,  3^3 = 27,  3^4 = 81,  3^5 = 243
#   4^2 = 16, 4^3 = 64,  4^4 = 256, 4^5 = 1024
#   5^2 = 25, 5^3 = 125, 5^4 = 625, 5^5 = 3125
#
#   If they are then placed in numerical order, with any repeats removed, we get
#   the following sequence of 15 distinct terms:
#   4, 8, 9, 16, 25, 27, 32, 64, 81, 125, 243, 256, 625, 1024, 3125.
#
#   How many distinct terms are in the sequence generated by a^b for 2 ≤ a ≤ N
#   and 2 ≤ b ≤ N?
#
# Input Format:
#   Input contains an integer N.
#
# Constraints:
#   2 ≤ N ≤ 10^5
#
# Output Format:
#   Print the answer corresponding to the test case.
#
# Sample Input:
#   5
#
# Sample Output:
#   15

function count_distinct_powers(n::Int)
    max_log = floor(Int, log2(n))

    # Precompute: for each max_power (1 to max_log), count unique exponents
    # in the union of {k×j : j ∈ [2,n]} for k = 1 to max_power
    unique_counts = Vector{Int}(undef, max_log)
    for max_power in 1:max_log
        seen = Set{Int}()
        for k in 1:max_power
            for j in 2:n
                push!(seen, k * j)
            end
        end
        unique_counts[max_power] = length(seen)
    end

    # Find primitive roots and sum their contributions
    is_perfect_power = falses(n)
    result = 0

    for base in 2:n
        if is_perfect_power[base]
            continue
        end

        # Count how many powers of base are ≤ n
        power_count = 1
        val = base
        while true
            next_val = val * base
            if next_val > n
                break
            end
            val = next_val
            power_count += 1
            is_perfect_power[val] = true
        end

        result += unique_counts[power_count]
    end

    return result
end

N = parse(Int, readline())
println(count_distinct_powers(N))
